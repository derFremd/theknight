10 REM **********************************
11 REM * ПРОГРАММА: THE KNIGHT (V2.0)   *
12 REM * АВТОР: СЕРГЕЙ С.               *
13 REM * ДАТА: 17.08.2021               *
14 REM * EMAIL: DER.FREMD@GMAIL.COM     *
15 REM * АЛГОРИТМ ИСПОЛЬЗУЕТ ПРАВИЛО    *
16 REM * ВАРНСДОРФА                     *
17 REM **********************************
20 CLS: SCREEN 0,0,64,128,16,208,6,134,22,54,0,197,34,192,2,152,82,173
25 COLOR 15,64,8: PLOT 6,6,2: LINE 249,249,B: PLOT STEP -3,-3,1: LINE STEP -237,-237,B
30 FOR I=0 TO 1: FOR J=0 TO 1
35 COLOR 6: PLOT 241*J,239*I,2: LINE STEP 14,16,BF: COLOR 15: LINE STEP -14,-16,B
40 XX=241*J:YY=239*I: GOSUB 3500
45 NEXT J: NEXT I
46 CUR 13,24: COLOR 241: PRINT " THE KNIGHT V2.0 "
60 COLOR 7: RESTORE 6000
63 L=22
65 READ A¤: IF A¤<>"." THEN CUR 2,L: PRINT A¤: L=L-1: GOTO 65
70 READ A¤: CUR 2,5: PRINT A¤;: GOSUB 3310
75 IF A¤<>"Y" THEN GOTO 1990
90 REM *** QUERY BATTLEFIELD AREA
100 CLEAR
101 DIM DD¤(3): DD¤(0)="!": DD¤(1)="/": DD¤(2)="-": DD¤(3)="\": REM *** PROGRESS ANIMATION
103 COLOR 15: LC=21: GOSUB 2110
105 CUR 2,22: PRINT "КАКОЙ РАЗМЕР ПОЛЯ БОЯ (МАКСИМУМ 12X8)"
110 CUR 2,21: INPUT "КЛЕТОК ПО ГОРИЗОНТАЛИ:",HS
120 CUR 2,20: INPUT "КЛЕТОК ПО ВЕРТИКАЛИ:",VS
122 HV=HS*VS: REM BATTLEFIELD AREA
125 IF HS>12 OR VS>8 THEN CUR 2,18: PRINT "СЛИШКОМ ПРОТЯЖЕННАЯ ТЕРРИТОРИЯ!": BEEP 0.3,10: PAUSE 3: GOTO 103
127 IF HS<2 OR VS<2 OR HV<6 THEN CUR 2,18: PRINT "НА ТАКОМ ПОЛЕ ПЕРЕМЕЩАТЬСЯ НЕЛЬЗЯ!": BEEP 0.3,10: PAUSE 3: GOTO 103
130 CUR 2,19: PRINT "ВЫ ВЫБРАЛИ ПОЛЕ";HS;"X";VS;"КЛЕТОК"
140 CUR 2,18: PRINT "УВЕРЕНЫ?";: GOSUB 3310
150 IF A¤<>"Y" THEN GOTO 100
151 REM *** DRAW BATTLEFIELD
152 COLOR 7: YY=VS-1: XX=0: GOSUB 2230: PLOT XC-1,YC-1,1
153 YY=-1: XX=HS: GOSUB 2230: LINE XC,YC,B
154 FOR YY=0 TO VS-1: FOR XX=0 TO HS-1: GOSUB 2250: NEXT XX: NEXT YY
155 COLOR 15: YY=-1: FOR XX=0 TO HS-1: GOSUB 2230: CUR 2,22
156 IF XX>=9 THEN PLOT XC-4,YC+2,2: GOTO 158
157 PLOT XC-1,YC+2,2
158 LINE 1,1,BS: PRINT XX+1: NEXT XX
159 XX=-1: FOR YY=0 TO VS-1: GOSUB 2230: CUR 2,22: PLOT XC+3,YC+4,2: LINE 1,1,BS: PRINT YY+1: NEXT YY
200 COLOR 15: LC=5: GOSUB 2110
205 REM *** QUERY - THE ATTACK BEGINS FROM
210 CUR 2,22: PRINT "ОТКУДА НАЧНЕМ АТАКУ?"
220 CUR 2,21: PRINT "X - СЛЕВА НАПРАВО, Y - СВЕРХУ ВНИЗ"
230 CUR 2,20: INPUT "ВВЕДИТЕ X,Y:",SX,SY
240 SX=SX-1: SY=SY-1: REM CORRECT, X & Y IS ZERO BASED
260 IF SX>=0 AND SX<HS AND SY>=0 AND SY<VS THEN GOTO 295
270 CUR 2,18: PRINT "НАПОМИНАЮ! ПОЛЕ БИТВЫ";HS;"НА";VS;"КЛЕТОК."
280 BEEP 0.3,10: PAUSE 2
290 GOTO 200
293 REM *** SHOW ATTACK BEGINS COORDINATE
295 XX=SX: YY=SY: GOSUB 2230: XX=XC: YY=YC: GOSUB 3500
298 COLOR 15
300 REM *** INITIALISATION
310 LC=5: GOSUB 2110: PRINT "ПОДОЖДИТЕ, ГОТОВИМ ВОЙСКО... ";
330 DIM FA(HV,9): REM BATTLEFIELD AREA
340 DIM JA(HV): REM MOVE CELL INDEX
350 DIM JC(HV): REM COPY OF JA(), BEST SOLUTION
360 FS=HV-1: REM MAX CELL INDEX
370 FP=FS-1: REM PENULTIMATE INDEX
390 JA(0)=HS*SY+SX: REM START INDEX
400 REM *** GENERATE AVAILABLE DIRECTIONS FOR ALL CELLS
410 FOR Y=0 TO VS-1
420 FOR X=0 TO HS-1
425 CUR 30,22: PRINT FS-ID
430 ID=Y*HS+X: REM COORD. TO INDEX
440 FA(ID,0)=0: REM EMPTY CELL
450 FOR J=1 TO 8: REM POSIBLE DIR.
460 ON J GOSUB 530, 540, 550, 560, 570, 580, 590, 600
470 IF XN<0 OR XN>=HS OR YN<0 OR YN>=VS THEN FA(ID,J)=-1: GOTO 490
480 FA(ID,J)=YN*HS+XN
490 NEXT J
500 NEXT X
510 NEXT Y
515 CUR 31,22: PRINT "ГОТОВО!"
520 GOTO 606
530 XN=X+2: YN=Y-1: RETURN
540 XN=X-2: YN=Y+1: RETURN
550 XN=X+2: YN=Y+1: RETURN
560 XN=X-2: YN=Y-1: RETURN
570 XN=X+1: YN=Y-2: RETURN
580 XN=X-1: YN=Y+2: RETURN
590 XN=X-1: YN=Y-2: RETURN
600 XN=X+1: YN=Y+2: RETURN
606 CUR 2,21: PRINT "ДА НАЧНЕТСЯ БИТВА! ТЕРПЕНИЕ [ ]"
607 CUR 2,19: PRINT "ЗЕМЕЛЬ ОСВОБОЖДЕНО -"
608 COLOR 14: CUR 10,1: PRINT "ПРОБЕЛ - ПРЕРВАТЬ АТАКУ": COLOR 15
610 REM *** MAIN CYCLE
620 CI=0: REM *** SET INIT. INDEX OF ARRAY JA()
625 CM=0: REM *** MAXIMUM CELL PROGRESS
627 DP=0: REM *** ANIMATION INDEX
630 CC=JA(CI): REM *** GET CURR CELL INDEX
640 GOSUB 3210: REM *** SHOW PROGRESS
650 IF CI=FS THEN GOTO 1510: REM *** LAST CELL?
652 IF INKEY¤="" THEN GOTO 670: REM *** ABORT ATTACKS?
654 CUR 2,18: PRINT "ПРЕРВАТЬ АТАКУ?";: GOSUB 3310
656 CUR 2,18: PRINT "                                  "
658 IF A¤="Y" THEN GOTO 1510
660 REM *** SEARCH NEXT FREE CELL WITH MIN JUMPS
670 NI=-1: REM *** NEXT INDEX
680 ME=8: REM *** MIN EMPTY CELLS AROUND
690 FI=0: REM *** INDEX INSIDE ARRAY FA()
700 FOR I=1 TO 8
710 PN=FA(CC,I): REM POSIBLE NEXT INDEX
720 IF PN<0 THEN GOTO 860: REM NO DIRECTION
730 IF FA(PN,0)<>0 THEN GOTO 860: REM OR NOT EMPTY
750 IF CI=FP THEN NI=PN: FI=I: GOTO 860
760 EC=0: REM *** COUNTER OF EMPTY CELLS AROUND
770 FOR J=1 TO 8
780 NN=FA(PN,J)
790 IF NN<0 THEN GOTO 820: REM *** NO DIR.
800 IF NN=CC THEN GOTO 820: REM *** CELL WE ARE FROM
810 IF FA(NN,0)=0 THEN EC=EC+1: REM ** INC. EMPTY CELLS
820 NEXT J
840 REM IF EC=0 THEN GOTO 860
850 IF ME>EC THEN NI=PN: ME=EC: FI=I
860 NEXT I
870 REM *** IS THERE CELL TO JUMP?
880 IF NI<0 THEN GOTO 950: REM *** NO, TRY STEP BACK
890 FA(CC,0)=FI: REM *** SAVE CURREN JUMP DIRECTION
900 CI=CI+1: REM *** INCREMENT INDEX OF JA()
910 JA(CI)=NI
920 CC=NI
925 REM GOSUB 2210: GOSUB 2230: XX=XC: YY=YC: GOSUB 3510
930 GOTO 640
940 REM *** TRY ROLLBACK
950 IF CI=0 THEN GOTO 1510: REM *** FIRST CELL?
955 REM GOSUB 2210: GOSUB 2240
960 JA(CI)=0: REM *** RESET CURRENT DIR.
970 CI=CI-1: REM *** STEP BACK INDEX
980 CC=JA(CI): REM *** STEP BACK CELL ID
990 EX=FA(CC,0): REM *** GET DIR. INDEX
1000 FA(CC,EX)=-1: REM *** WRONG DIR, FORGET
1010 FA(CC,0)=0: REM *** RESET DIRECTION INDEX
1030 GOTO 640
1500 REM *** CHECK RESULT
1510 CUR 31,21: PRINT "☼": CUR 2,20
1515 IF CI=FS THEN GOTO 1540
1520 PRINT "УВЫ, ОСВОБОЖДЕНО ТОЛЬКО";INT((CM+1)*100/HV);"% ЗЕМЕЛЬ."
1530 GOTO 1550
1540 PRINT "ПОЗДРАВЛЯЮ! МЫ ОСВОБОДИЛИ ВСЕ ЗЕМЛИ."
1550 CUR 2,19: PRINT "ВСЕГО ПЕРЕМЕЩЕНИЙ КОННИЦЫ:";(CM+1)
1560 REM *** SHOW BEST RESULT
1570 FOR CI=0 TO CM: CUR 31,21: CC=JC(CI)
1575 GOSUB 2210: GOSUB 2240: GOSUB 2230
1576 IF CI=CM THEN XX=XC: YY=YC: GOSUB 3510: GOTO 1590
1580 IF CI<9 THEN PLOT XC-1,YC+4,2: GOTO 1583
1582 PLOT XC-4,YC+4,2
1583 IF CI=0 THEN COLOR 4: GOTO 1585
1584 COLOR 7: CUR 2,22
1585 LINE 1,1,BS: PRINT (CI+1)
1590 NEXT CI
1960 GOSUB 2040
1970 LC=5: GOSUB 2100: COLOR 15: PRINT "В НОВЫЙ ПОХОД?";: GOSUB 3310
1980 IF A¤="Y" THEN COLOR 0: PLOT 17,180,1: LINE 238,17,BF: GOTO 100
1990 CLS: COLOR 15: CUR 14,14: PRINT "ДО НОВЫХ БИТВ!": GOTO 8000
2000 REM ***************************
2010 REM *** USEFULL SUBROUTINES ***
2020 REM ***************************
2030 REM *** WAIT ANY KEY
2040 COLOR 6: CUR 9,1: PRINT "  ▶ ЖМИ ЛЮБУЮ КНОПКУ ◀  ";
2060 IF INKEY¤="" THEN GOTO 2060
2070 CUR 11,1: PRINT "                    ";
2090 RETURN
2100 REM *** TO CLEAR TEXT
2110 FOR LL= 1 TO LC
2120 CUR 2,23-LL: PRINT "                                      "
2140 NEXT LL
2150 CUR 2,22
2160 RETURN
2200 REM *** COORD OF THE CELL FROM INDEX CC
2210 YY=INT(CC/HS): XX=CC-HS*YY: RETURN
2220 REM *** COORD AT SCR FROM XX & YY COORDS
2230 XC=128-HS*8+XX*16: YC=150-YY*16: RETURN
2240 REM *** DRAW ONE CELL USING XX,YY
2250 YX=YY+XX
2260 IF (YX-INT(YX/2)*2)=0 THEN COLOR 2: GOTO 2280
2270 COLOR 10
2280 GOSUB 2230: PLOT XC,YC,1: LINE STEP 15,15,BF: RETURN
3200 REM *** SHOW PROGRESS
3210 IF CM<CI THEN CM=CI: FOR II=0 TO CM: JC(II)=JA(II): NEXT II
3220 COLOR 15: CUR 31,21: PRINT DD¤(DP): DP=DP+1: IF DP>3 THEN DP=0
3230 CUR 22,19: PRINT INT((CM+1)*100/HV);"%"
3240 RETURN
3300 REM *** QUESTION Y/N
3310 INPUT " (Y-ДА,N-НЕТ):",A¤:RETURN
3500 REM *** PAINT THE KNIGHT
3510 RESTORE 7000
3520 READ C: IF C<0 THEN RETURN
3530 PLOT XX,YY,2: ON C GOSUB 3550,3560,3570
3540 GOTO 3520
3550 READ C: COLOR C: RETURN
3560 READ X,Y: PLOT STEP X,Y,1: RETURN
3570 READ X1,Y1,X2,Y2: PLOT STEP X1,Y1,1: LINE STEP X2,Y2: RETURN
6000 DATA "ДОБРОГО ЗДРАВИЯ, ВЕЛИКИЙ ПРЕДВОДИТЕЛЬ!"
6005 DATA "ДАВНЫМ ДАВНО, А ТОЧНЕЕ ПОСЛЕ ДОЖДИКА В"
6010 DATA "ЧЕТВЕРГ И НЕПОСРЕДСТВЕННО ПЕРЕД ТЕМ   "
6020 DATA "КAК РАК НА ГОРЕ СВИСНУЛ, НАШИ ПЛОДО-  "
6030 DATA "РОДНЫЕ ЗЕМЛИ ЗАХВАТИЛ ЗЛОЙ МАГ И      "
6040 DATA "ВОЛШЕБНИК - BISHOP VI.                "
6050 DATA "ДЛЯ ТОГО ЧТОБЫ ОСВОБОДИТЬ ЭТИ ЗЕМЛИ   "
6060 DATA "НАМ ПОНАДОБИТСЯ ТВОЯ ПОМОЩЬ - ТВОЯ    "
6070 DATA "ВОЛШЕБНАЯ КОННИЦА. НО ЧТОБЫ ПОБЕДИТЬ  "
6080 DATA "ЭТОГО МАГА НЕОБХОДИМО СЛЕДОВАТЬ НЕКО- "
6090 DATA "ТОРЫМ ПРАВИЛАМ. ТВОЯ КОННИЦА ДОЛЖНА   "
6100 DATA "НАСТУПАТЬ НА ВРАГА, ПЕРЕМЕЩАЯСЬ КАК   "
6110 DATA "ШАХМАТНАЯ ФИГУРА THE KNIGHT. ТОЛЬКО   "
6120 DATA "ТОГДА ЧАРЫ ЭТОГО МАГА РАССЕЮТСЯ, ЕГО  "
6130 DATA "ВОИНЫ СТАНУТ УЯЗВИМЫ И ПАДУТ ОТ НАШИХ "
6140 DATA "СТРЕЛ И МЕЧЕЙ."
6150 DATA ".","ТЫ ГОТОВ НАМ ПОМОЧЬ?"
7000 DATA 1,8,3,2,1,10,0,3,3,2,8,0,3,4,3,6,0,3,3,4,8,0
7050 DATA 3,4,5,6,0,3,4,6,6,0,3,5,7,6,0,3,6,8,5,0
7080 DATA 3,7,9,4,0,3,2,9,1,0,3,7,10,4,0,3,2,10,3,0
7120 DATA 3,7,10,4,0,3,3,11,7,0,3,4,12,6,0,3,5,13,4,0
7150 DATA 3,6,14,1,0,3,5,15,1,0,1,3,2,5,12,1,14,3,4,2,2,0
7200 DATA 2,10,2,3,5,4,4,0,3,5,6,2,2,3,10,8,0,1,2,9,12,-1
8000 REM **********************
8002 REM *** END OF PROGRAM ***
8004 REM **********************

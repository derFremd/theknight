10 REM ********************************
11 REM * ПРОГРАММА: THE KNIGHT (V1.0) *
12 REM * АВТОР: СЕРГЕЙ С.             *
13 REM * ДАТА: 10.08.2021             *
14 REM * EMAIL: DER.FREMD@GMAIL.COM   *
15 REM ********************************
20 CLS: SCREEN 0,0,64,128,16,208,6,134,22,54,0,197,34,192,2,152,82,173
25 COLOR 15,64,8: PLOT 6,6,2: LINE 249,249,B: PLOT STEP -3,-3,1: LINE STEP -237,-237,B
30 FOR I=0 TO 1: FOR J=0 TO 1
35 COLOR 6: PLOT 241*J,239*I,2: LINE STEP 14,16,BF: COLOR 15: LINE STEP -14,-16,B
40 XX=241*J:YY=239*I: GOSUB 3500
45 NEXT J: NEXT I
60 COLOR 7: RESTORE 6000
63 L=22
65 READ A¤: IF A¤<>"." THEN CUR 2,L: PRINT A¤: L=L-1: GOTO 65
70 READ A¤: CUR 2,5: PRINT A¤;: GOSUB 3310
75 IF A¤<>"Y" THEN GOTO 1990
90 REM LABEL FIELD:
100 CLEAR: COLOR 15
103 LC=21: GOSUB 2110
105 CUR 2,22: PRINT "УКАЖИТЕ РАЗМЕР ПОЛЯ БОЯ."
110 CUR 2,21: INPUT "КЛЕТОК ПО ГОРИЗОНТАЛИ:",HU
120 CUR 2,20: INPUT "КЛЕТОК ПО ВЕРТИКАЛИ:",VU
125 IF (HU*VU)>64 THEN CUR 2,18: PRINT "СЛИШКОМ БОЛЬШАЯ ТЕРРИТОРИЯ!": BEEP 0.3,10: PAUSE 3: GOTO 103
127 IF HU<2 OR VU<2 OR (HU*VU)<6 THEN CUR 2,18: PRINT "НА ТАКОМ ПОЛЕ ПЕРЕМЕЩАТЬСЯ НЕЛЬЗЯ!": BEEP 0.3,10: PAUSE 3: GOTO 103
130 CUR 2,19: PRINT "ВЫ ВЫБРАЛИ ПОЛЕ";HU;"X";VU;"КЛЕТОК"
140 CUR 2,18: PRINT "УВЕРЕНЫ?";: GOSUB 3310
150 IF A¤<>"Y" THEN GOTO 100
151 REM DRAW SHESS FIELD
152 COLOR 7: YY=VU-1: XX=0: GOSUB 2230: PLOT XC-1,YC-1,1
153 YY=-1: XX=HU: GOSUB 2230: LINE XC,YC,B
155 FOR YY=0 TO VU-1: FOR XX=0 TO HU-1: GOSUB 2250: NEXT XX: NEXT YY
156 COLOR 15: YY=-1: FOR XX=0 TO HU-1: GOSUB 2230: PLOT XC-1,YC+2,2: LINE 1,1,BS: PRINT XX+1: NEXT XX
158 XX=-1: FOR YY=0 TO VU-1: GOSUB 2230: PLOT XC+3,YC+4,2: LINE 1,1,BS: PRINT YY+1: NEXT YY
160 HS=HU+4: REM + БОРДЮР КОЛЛИЗИЙ ПО 2 КЛЕТКИ СПРАВА/СЛЕВА
170 VS=VU+4: REM АНАЛОГИЧНО ПО ВЕРТИКАЛИ
190 REM LABEL FROMXY:
200 COLOR 15: LC=5: GOSUB 2110
210 CUR 2,22: PRINT "ОТКУДА НАЧНЕМ АТАКУ?"
220 CUR 2,21: PRINT "X - СЛЕВА НАПРАВО, Y - СВЕРХУ ВНИЗ"
230 CUR 2,20: INPUT "ВВЕДИТЕ X,Y:",SX,SY
240 SX=SX-1: SY=SY-1: REM ZERO BASED
260 IF SX>=0 AND SX<HU AND SY>=0 AND SY<VU THEN GOTO 295
270 CUR 2,18: PRINT "НАПОМИНАЮ! ПОЛЕ БИТВЫ";HU;"НА";VU;"КЛЕТОК."
280 BEEP 0.3,10: PAUSE 2
290 GOTO 200
295 XX=SX: YY=SY: GOSUB 2230: XX=XC: YY=YC: GOSUB 3500
298 COLOR 15
300 REM *** INITIALISATION
310 LC=5: GOSUB 2110: PRINT "ПОДОЖДИТЕ, ГОТОВИМ ВОЙСКО... ";
330 SI=HS*(SY+2)+SX+2: REM BEGINING INDEX IN ARRAY FA()
340 JI=HU*VU: REM SIZE OF ARREY OF JUMPING INDEXES 
350 FS=HS*VS
360 DIM FA(FS)
370 REM FILLING FILL ARRAY: 0=EMPTY, (1-8)=DIRECTION, 9=COLLISION
380 FOR Y=0 TO VS-1
390 FOR X=0 TO HS-1
400 IF X<2 OR X>=(HS-2) OR Y<2 OR Y>=(VS-2) THEN FA(X+HS*Y)=9
410 NEXT X
420 NEXT Y
500 FA(SI)=1: REM SET START DIRECTION
510 JI=HU*VU
520 DIM JA(JI): REM MOVE CELL INDEX
525 DIM JC(JI): REM COPY OF JA(), MAX CELLS WHICH WERE GOT
530 JA(0)=SI
540 DIM DA(9)
550 DA(1)=-(HS-2): REM RIGHT-TO
560 DA(2)=HS-2: REM LEFT-DOWN
570 DA(3)=HS+2: REM RIGHT-DOWN
580 DA(4)=-(HS+2): REM LEFT-UP
590 DA(5)=-(HS*2-1): REM UP-RIGHT
600 DA(6)=HS*2-1: REM DOWN-LEFT
610 DA(7)=-(HS*2+1): REM UP-LEFT
620 DA(8)=HS*2+1: REM DOWN-RIGHT
630 CI=0: REM INIT MOVING ARRAY INDEX OF JA()
635 CM=0: REM PROGRESS MAXIMUM
637 IC=0: REM ITERATION COUNTER
640 DIM DD¤(3): DD¤(0)="!": DD¤(1)="/": DD¤(2)="-": DD¤(3)="\": DP=0
650 PRINT "ПОЕХАЛИ"
660 CUR 2,21: PRINT "ДА НАЧНЕТСЯ БИТВА! ТЕРПЕНИЕ [ ]"
670 CUR 2,19: PRINT "ЗЕМЕЛЬ ОСВОБОЖДЕНО -"
680 COLOR 14: CUR 10,1: PRINT "ПРОБЕЛ - ПРЕРВАТЬ АТАКУ"
685 COLOR 15
690 REM *** LABEL WORK:     
700 IF INKEY¤="" THEN GOTO 705
702 CUR 2,18: PRINT "ПРЕРВАТЬ АТАКУ?";: GOSUB 3310
703 CUR 2,18: PRINT "                                  "
704 IF A¤="Y" THEN GOTO 1510
705 GOSUB 3210
710 CC=JA(CI): REM CURRENT CELL INDEX
720 DI=FA(CC): REM CURRENT DIRECTION INDEX
725 REM *** LABEL OFFSET:
730 DL=DA(DI): REM INDEX OFFSET
740 NI=CC+DL: REM NEXT CELL INDEX
750 REM IS THE NEXT CELL EMPTY?
760 IF FA(NI)<>0 THEN GOTO 900: REM NO
770 REM THE NEXT CELL IS EMPTY
780 FA(NI)=1: REM INIT NEXT EMPTY CELL DIRECTION
790 CI=CI+1: REM INCRIMENT MOVING ARRAY INDEX
800 JA(CI)=NI: REM SET NEXT EMPTY CELL INDEX
810 REM IS THIS CELL LAST?
820 IF (CI+1)<JI THEN GOTO 700: REM NO
830 GOSUB 3210: GOTO 1510: REM YES
890 REM *** LABEL NOEMPT
900 REM IS THERE OTHER DIRECTION?
910 IF DI<8 THEN GOTO 1050: REM YES
920 REM NO. CAN WE GET ONE STEP BACK?
930 IF CI=0 GOTO 1510: REM NO
940 REM YES, GET ONE STEP BACK
950 JA(CI)=0
960 FA(CC)=0
970 CI=CI-1
980 CC=JA(CI)
990 DI=FA(CC)
1000 GOSUB 3210
1010 GOTO 900
1040 REM *** LABEL NEWDIR:
1050 DI=DI+1
1060 FA(CC)=DI
1070 GOTO 730
1500 REM *** LABEL RESULT:
1510 CUR 2,20:
1515 IF (CI+1)=JI THEN GOTO 1540
1520 PRINT "УВЫ, ОСВОБОЖДЕНО ТОЛЬКО";INT((CM+1)*100/JI);"% ЗЕМЕЛЬ."
1530 GOTO 1550
1535 REM *** LABEL SOL
1540 PRINT "ПОЗДРАВЛЯЮ! МЫ ОСВОБОДИЛИ ВСЕ ЗЕМЛИ."
1550 CUR 2,19: PRINT "ВСЕГО ПЕРЕМЕЩЕНИЙ КОННИЦЫ:";(CM+1)
1560 REM *** SHOW BEST RESULT
1570 FOR CI=0 TO JI-1: CUR 31,21: CC=JC(CI)
1572 IF CC=0 GOTO 1590
1575 GOSUB 2210: GOSUB 2240: GOSUB 2230
1576 IF CI=JI-1 OR JC(CI+1)=0 THEN XX=XC: YY=YC: GOSUB 3510: GOTO 1590
1580 IF CI<9 THEN PLOT XC-1,YC+4,2: GOTO 1583
1582 PLOT XC-4,YC+4,2
1583 IF CI=0 THEN COLOR 4: GOTO 1585
1584 COLOR 7
1585 LINE 1,1,BS: PRINT (CI+1)
1590 NEXT CI
1960 GOSUB 2040
1970 LC=5: GOSUB 2100: COLOR 15: PRINT "В НОВЫЙ ПОХОД?";: GOSUB 3310
1980 IF A¤="Y" THEN COLOR 0: PLOT 17,180,1: LINE 238,17,BF: GOTO 100
1990 CLS: COLOR 15: PRINT "ДО НОВЫХ БИТВ!": GOTO 8000
2000 REM ***************************
2010 REM *** USEFULL SUBROUTINES ***
2020 REM ***************************
2030 REM *** WAIT ANY KEY ***
2040 COLOR 6: CUR 9,1: PRINT "  ▶ ЖМИ ЛЮБУЮ КНОПКУ ◀  ";
2060 IF INKEY¤="" THEN GOTO 2060
2070 CUR 11,1: PRINT "                    ";
2090 RETURN
2100 REM *** TO CLEAR TEXT ***
2110 FOR LL= 1 TO LC
2120 CUR 2,23-LL: PRINT "                                      "
2140 NEXT LL
2150 CUR 2,22
2160 RETURN
2200 REM *** COORD OF THE CELL FROM INDEX CC
2210 YY=INT(CC/HS)-2: XX=CC-HS*(YY+2)-2: RETURN
2220 REM *** COORD AT SCR FROM XX & YY COORDS
2230 XC=128-HU*8+XX*16: YC=150-YY*16: RETURN
2240 REM *** DRAW ONE CELL USING XX,YY
2250 YX=YY+XX
2260 IF (YX-INT(YX/2)*2)=0 THEN COLOR 2: GOTO 2280
2270 COLOR 10
2280 GOSUB 2230: PLOT XC,YC,1: LINE STEP 15,15,BF: RETURN
3200 REM *** LABEL SHOW PROGRESS
3210 IF CM<CI THEN CM=CI: FOR II=0 TO CM: JC(II)=JA(II): NEXT II
3215 IC=IC+1: II=II+1: COLOR8:PLOT159,255,1:LINESTEP-64,0:COLOR15:LINESTEPCI,0:PLOT95+CM,255,1:IF II<10 THEN RETURN
3217 II=0
3220 CUR 31,21: PRINT DD¤(DP): DP=DP+1: IF DP>3 THEN DP=0
3230 CUR 22,19: PRINT INT((CM+1)*100/JI);"%"
3240 RETURN
3300 REM *** QUESTION Y/N
3310 INPUT " (Y-ДА,N-НЕТ):",A¤:RETURN
3500 REM *** PAINT THE KNIGHT
3510 RESTORE 7000
3520 READ C: IF C<0 THEN RETURN
3530 PLOT XX,YY,2: ON C GOSUB 3550,3560,3570
3540 GOTO 3520
3550 READ C: COLOR C: RETURN
3560 READ X,Y: PLOT STEP X,Y,1: RETURN
3570 READ X1,Y1,X2,Y2: PLOT STEP X1,Y1,1: LINE STEP X2,Y2: RETURN
6000 DATA "ДОБРОГО ЗДРАВИЯ, ВЕЛИКИЙ ПРЕДВОДИТЕЛЬ!"
6005 DATA "ДАВНЫМ ДАВНО, А ТОЧНЕЕ ПОСЛЕ ДОЖДИКА В"
6010 DATA "ЧЕТВЕРГ И НЕПОСРЕДСТВЕННО ПЕРЕД ТЕМ   "
6020 DATA "КAК РАК НА ГОРЕ СВИСНУЛ, НАШИ ПЛОДО-  "
6030 DATA "РОДНЫЕ ЗЕМЛИ ЗАХВАТИЛ ЗЛОЙ МАГ И      "
6040 DATA "ВОЛШЕБНИК - BISHOP VI.                "
6050 DATA "ДЛЯ ТОГО ЧТОБЫ ОСВОБОДИТЬ ЭТИ ЗЕМЛИ   "
6060 DATA "НАМ ПОНАДОБИТСЯ ТВОЯ ПОМОЩЬ - ТВОЯ    "
6070 DATA "ВОЛШЕБНАЯ КОННИЦА. НО ЧТОБЫ ПОБЕДИТЬ  "
6080 DATA "ЭТОГО МАГА НЕОБХОДИМО СЛЕДОВАТЬ НЕКО- "
6090 DATA "ТОРЫМ ПРАВИЛАМ. ТВОЯ КОННИЦА ДОЛЖНА   "
6100 DATA "НАСТУПАТЬ НА ВРАГА, ПЕРЕМЕЩАЯСЬ КАК   "
6110 DATA "ШАХМАТНАЯ ФИГУРА THE KNIGHT. ТОЛЬКО   "
6120 DATA "ТОГДА ЧАРЫ ЭТОГО МАГА РАССЕЮТСЯ, ЕГО  "
6130 DATA "ВОИНЫ СТАНУТ УЯЗВИМЫ И ПАДУТ ОТ НАШИХ "
6140 DATA "СТРЕЛ И МЕЧЕЙ."
6150 DATA ".","ТЫ ГОТОВ НАМ ПОМОЧЬ?"
7000 DATA 1,8,3,2,1,10,0,3,3,2,8,0,3,4,3,6,0,3,3,4,8,0
7050 DATA 3,4,5,6,0,3,4,6,6,0,3,5,7,6,0,3,6,8,5,0
7080 DATA 3,7,9,4,0,3,2,9,1,0,3,7,10,4,0,3,2,10,3,0
7120 DATA 3,7,10,4,0,3,3,11,7,0,3,4,12,6,0,3,5,13,4,0
7150 DATA 3,6,14,1,0,3,5,15,1,0,1,3,2,5,12,1,14,3,4,2,2,0
7200 DATA 2,10,2,3,5,4,4,0,3,5,6,2,2,3,10,8,0,1,2,9,12,-1
8000 REM **********************
8002 REM *** END OF PROGRAM ***
8004 REM **********************
